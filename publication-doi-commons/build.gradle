

dependencies {
    implementation group: 'com.github.BIBSYSDEV', name: 'nva-datamodel-java', version: project.ext.nvaDatamodelVersion

    api group: 'com.github.bibsysdev', name: 'core', version: project.ext.nvaCommonsVersion
    api(group: 'com.amazonaws', name: 'aws-lambda-java-events', version: project.ext.nvaAwsLambdaJavaEvents) {
        because('Contains DynamodbEvent.DynamodbStreamRecord')
    }

    testImplementation project(":publication-testing")
}

configurations.testImplementation.canBeResolved = true

task copyNativeDeps(type: Copy) {
    from(configurations.testImplementation) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/dynamodb-local'
}

test.dependsOn copyNativeDeps
test.doFirst {
    systemProperty "java.library.path", 'build/dynamodb-local'
}

// Build and package publication-commons and dependencies for Lambda Layer

configurations.api.canBeResolved = true

task copyDepsForLayer(type: Copy) {
    from(configurations.api) {
    }
    into 'build/libs'
}

task buildZipForLayer(type: Zip) {
    from('build/libs') {
    }
    into('java/lib')
}

buildZipForLayer.dependsOn copyDepsForLayer
build.dependsOn buildZipForLayer