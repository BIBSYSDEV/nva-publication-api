repositories {
    // Repository for running Embedded DynamoDB locally.
    maven { url "https://s3.eu-central-1.amazonaws.com/dynamodb-local-frankfurt/release" }
}

dependencies {
    compileOnly project(':publication-commons')
    implementation project(":publication-events")
    implementation(group: 'com.github.BIBSYSDEV.nva-datacite-mds', name: 'event-handler-template', version: '0.0.1') {
        exclude group: 'com.github.BIBSYSDEV', module: 'nva-commons'
        because 'first usage of event-handler-template framework/library, it is high candidate to be moved into its own repository if the first test has been a success.'
    }

    testImplementation project(':publication-commons')
    testImplementation project(":publication-testing")
    testImplementation('com.amazonaws:DynamoDBLocal:1.12.0') {
        exclude group: 'com.amazonaws', module: 'aws-java-sdk-dynamodb'
        exclude group: 'com.amazonaws', module: 'aws-java-sdk-core'
    }
}

configurations.testImplementation.canBeResolved = true

task copyNativeDeps(type: Copy) {
    from(configurations.testImplementation) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/dynamodb-local'
}

test.dependsOn copyNativeDeps
test.doFirst {
    systemProperty "java.library.path", 'build/dynamodb-local'
}