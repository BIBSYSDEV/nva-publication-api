repositories {
    maven { url "https://s3.eu-central-1.amazonaws.com/dynamodb-local-frankfurt/release" }
}

def jacksonVersion = '2.10.2'
def awsSdkVersion = '1.11.714'

dependencies {
    api group: 'com.github.BIBSYSDEV', name: 'nva-commons', version: 'v0.3.4'
    api group: 'com.github.BIBSYSDEV', name: 'nva-datamodel-java', version: 'd94a62b'
    api group: 'io.mikael', name: 'urlbuilder', version: '2.0.9'
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    api group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    api group: 'org.zalando', name: 'problem', version: '0.23.0'
    api group: 'org.zalando', name: 'jackson-datatype-problem', version: '0.23.0'
    api group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.0'
    api group: 'com.amazonaws', name: 'aws-java-sdk-lambda', version: awsSdkVersion
    api group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb', version: awsSdkVersion

    testImplementation('com.amazonaws:DynamoDBLocal:1.12.0') {
        exclude group: 'com.amazonaws', module: 'aws-java-sdk-dynamodb'
        exclude group: 'com.amazonaws', module: 'aws-java-sdk-core'
    }
}

configurations.testImplementation.canBeResolved = true

task copyNativeDeps(type: Copy) {
    from(configurations.testImplementation) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/dynamodb-local'
}

test.dependsOn copyNativeDeps
test.doFirst {
    systemProperty "java.library.path", 'build/dynamodb-local'
}

// Build and package publication-commons and dependencies for Lambda Layer

configurations.api.canBeResolved = true

task copyDepsForLayer(type: Copy) {
    from(configurations.api) {
    }
    into 'build/libs'
}

task buildZipForLayer(type: Zip) {
    from('build/libs') {
    }
    into('java/lib')
}

buildZipForLayer.dependsOn copyDepsForLayer
build.dependsOn buildZipForLayer