PREFIX nva: <https://nva.sikt.no/ontology/publication#>
PREFIX project: <https://example.org/project-ontology.ttl#>

CONSTRUCT {
    <%s> nva:funding ?funding .
    ?funding a ?type ;
    nva:source ?source ;
    nva:identifier ?identifier ;
    nva:label ?label .
    ?source a nva:FundingSource ;
    nva:identifier ?sourceIdentifier ;
    nva:label ?sourceLabel .
} WHERE {
    SELECT ?funding ?type ?source ?identifier ?label ?sourceIdentifier ?sourceLabel
    WHERE {
        {
            SELECT (MIN(?otherFunding) as ?funding) ?type ?source ?identifier ?label
            WHERE {
                {
                    [] project:funding ?otherFunding .
                    ?otherFunding a ?rawType .
                    OPTIONAL { ?otherFunding project:source ?source . }
                    OPTIONAL { ?otherFunding project:identifier ?identifier . }
                    OPTIONAL { ?otherFunding project:label ?label . }
                } UNION {
                    [] nva:funding ?otherFunding .
                    ?otherFunding a ?rawType .
                    OPTIONAL { ?otherFunding nva:source ?source . }
                    OPTIONAL { ?otherFunding nva:identifier ?identifier . }
                    OPTIONAL { ?otherFunding nva:label ?label . }
                }
                BIND(IRI(REPLACE(STR(?rawType), STR(project:), STR(nva:))) AS ?type)
            }
            GROUP BY ?type ?source ?identifier ?label
        }

        OPTIONAL { ?source a nva:FundingSource . }
        OPTIONAL { ?source nva:identifier ?sourceIdentifier . }
        OPTIONAL { ?source nva:label ?sourceLabel . }
    }
}