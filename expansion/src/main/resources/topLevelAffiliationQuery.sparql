#Constructs an organization with a topLevelAffiliation field determined by recursively looking in
#the orgs partOf-field until an org isn't part of another org.

PREFIX  organization: <https://bibsysdev.github.io/src/nva/ontology.ttl#>

CONSTRUCT {
  ?org a organization:Organization ;
    organization:name ?org_name ;
    organization:topLevelAffiliation ?top_org .
  ?top_org a organization:Organization ;
    organization:name ?top_org_name
  } WHERE {
  ?org a organization:Organization ;
    organization:name ?org_name ;
    organization:partOf* ?top_org .
  ?top_org organization:name ?top_org_name .
    FILTER NOT EXISTS { ?top_org organization:partOf ?e } .
}