#Constructs TODO: explain

PREFIX  nva: <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
  ?publication nva:nviType ?nviCandidate .
} WHERE {
  ?publication a nva:Publication .
    ?publication nva:status ?publicationStatus .
    ?publication nva:entityDescription/nva:reference/nva:publicationInstance/rdf:type ?instanceType .

    OPTIONAL { ?publication nva:entityDescription/nva:publicationDate/nva:year ?publicationYear . }

    OPTIONAL { ?publication nva:entityDescription/nva:reference/nva:publicationContext/nva:publisher/nva:level ?publisherLevel . }
    OPTIONAL { ?publication nva:entityDescription/nva:reference/nva:publicationContext/nva:series/nva:level ?seriesLevel . }
    OPTIONAL { ?publication nva:entityDescription/nva:reference/nva:publicationContext/nva:journal/nva:level ?journalLevel .  }

    BIND(IF (BOUND(?seriesLevel), ?seriesLevel, ?publisherLevel) AS ?seriesOrPublisherLevel)
    BIND(IF (BOUND(?journalLevel), ?journalLevel, ?seriesOrPublisherLevel) AS ?optionalLevel)
    BIND(IF (BOUND(?optionalLevel), ?optionalLevel, "0") AS ?level)

    BIND(IF (?instanceType = nva:AcademicMonograph
             || ?instanceType = nva:AcademicChapter
             || ?instanceType = nva:AcademicArticle
             || ?instanceType = nva:AcademicLiteratureReview, "true"^^xsd:boolean, "false"^^xsd:boolean) AS ?isNviInstance)

    BIND(IF (?publicationStatus = nva:PUBLISHED
      && ?isNviInstance = "true"^^xsd:boolean
      && ?publicationYear = "2023"
      && (?level = "1" || ?level = "2"), nva:NviCandidate, nva:NonNviCandidate)  AS ?nviCandidate)
} GROUP BY ?publication ?nviCandidate