#Constructs nviType as either NviCandidate or NonNviCandidate based on the publication status,
#publicationDate, publicationInstance and publicationContext levels.

PREFIX  nva: <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
  ?publication nva:nviType ?nviCandidate .
} WHERE {

  ?publication a nva:Publication .
    ?publication nva:status ?publicationStatus .
    ?publication nva:entityDescription/nva:reference/nva:publicationInstance/rdf:type ?instanceType .

    OPTIONAL {
      SELECT distinct ?publicationYear WHERE {
        ?publicationDate rdf:type ?type .
        ?publicationDate nva:year ?publicationYear
        FILTER(?type = <https://nva.sikt.no/ontology/publication#PublicationDate>)
      }
    }

    OPTIONAL {
      SELECT distinct ?publisherLevel WHERE {
        ?publisher rdf:type ?type .
        ?publisher nva:level ?publisherLevel
        FILTER(?type = <https://nva.sikt.no/ontology/publication#Publisher>)
      }
    }

    OPTIONAL {
      SELECT distinct ?seriesLevel WHERE {
        ?publisher rdf:type ?type .
        ?publisher nva:level ?seriesLevel
        FILTER(?type = <https://nva.sikt.no/ontology/publication#Series>)
      }
    }

    OPTIONAL {
      SELECT distinct ?journalLevel WHERE {
            ?publisher rdf:type ?type .
            ?publisher nva:level ?journalLevel
            FILTER(?type = <https://nva.sikt.no/ontology/publication#Journal>)
      }
    }

    BIND(IF (BOUND(?seriesLevel), ?seriesLevel, ?publisherLevel) AS ?seriesOrPublisherLevel)
    BIND(IF (BOUND(?journalLevel), ?journalLevel, ?seriesOrPublisherLevel) AS ?optionalLevel)
    BIND(IF (BOUND(?optionalLevel), ?optionalLevel, "0") AS ?level)

    BIND(IF (?instanceType = nva:AcademicMonograph
             || ?instanceType = nva:AcademicChapter
             || ?instanceType = nva:AcademicArticle
             || ?instanceType = nva:AcademicLiteratureReview, "true"^^xsd:boolean, "false"^^xsd:boolean) AS ?isNviInstance)

    BIND(IF (?publicationStatus = nva:PUBLISHED
      && ?isNviInstance = "true"^^xsd:boolean
      && ?publicationYear = "__NVI_YEAR__"
      && (?level = "1" || ?level = "2"), nva:NviCandidate, nva:NonNviCandidate)  AS ?nviCandidate)
} GROUP BY ?publication ?nviCandidate